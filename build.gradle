apply plugin: 'java'
apply plugin: 'java-library-distribution'

distributions {
    main{
        baseName = 'classseven_two'
    }
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'src.main.java.com.vgorcinschi.classseven_two.main.ClasssevenApplication'
}

task wrapper(type: Wrapper){
    gradleVersion='2.14'
}

jar{
    baseName = 'classseven_two'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}


dependencies {
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.5'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.5'
    runtime group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
}

ext.versionFile = file('version.properties')

task loadVersion {
    project.version = readVersion()
}

//version = new ProjectVersion(0,1)

ProjectVersion readVersion(){
    if(!versionFile.exists()){
        throw new GradleException("Required version file does not exist:\n\
$versionFile.canonicalPath")
    }
    
    Properties versionProps = new Properties()
    
    versionFile.withInputStream {
        stream -> versionProps.load(stream)
    }
    
    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(),
        versionProps.release.toBoolean())
}

class ProjectVersion {
    Integer major
    Integer minor
    Boolean release
    
    ProjectVersion(Integer major, Integer minor){
        this.major=major
        this.minor=minor
        this.release=Boolean.FALSE
    }
    
    ProjectVersion(Integer major, Integer minor, Boolean release){
        this.major=major
        this.minor=minor
        this.release=release
    }
    
    @Override
    String toString(){
        "$major.$minor${release ? '' : '-SNAPSHOT'}"
    }
}

task printVersion << {
    logger.quiet "Version: $version"
}

task incrementMajorVersion(group: 'versioning', description: 'Increments project \n\
major version.') << {
    String currentVersion = version.toString()
    ++version.major
    String newVersion = version.toString()
    logger.info "Increment major project version: $currentVersion -> $newVersion"
    
    ant.propertyfile(file: versionFile) {
        entry(key: 'major', type: 'int', operation: '+', value: 1)
    }
}

task incrementMinorVersion(group: 'versioning', description: 'Increments project \n\
minor version.') << {
    String currentVersion = version.toString()
    ++version.minor
    String newVersion = version.toString()
    logger.info "Increment minor project version: $currentVersion -> $newVersion"
    
    ant.propertyfile(file: versionFile) {
        entry(key: 'minor', type: 'int', operation: '+', value: 1)
    }
}
